
# @COPYRIGHT@
# Licensed under MIT license (LICENSE.txt).

Import( 'env' )

from SCons.Util import CLVar

# "Generate" __init__.py
init_py = env.Substfile(['__init__.py.in'], SUBST_DICT = {})

modules = [
    'types_mod',
    'functions_mod',
    'exception_mod',
    'platform_mod',
    'context_mod',
    'device_partition_mod',
#    TODO: uncomment program_mod once it's ready
#    'program_mod'
]
libs = env.get('SWIGPY_LIBS', CLVar()) + [ 'clxx_cl', 'OpenCL' ]
# Build modules
modfiles = env.SwigPyModule(modules, SWIGPY_LIBS = libs)
instfiles =  [m for m in modfiles if not str(m).endswith('.cc')] + init_py

# FIXME: this is only for now, elaborate what should be the destination path,
#        also the python packages should be compiled when installed, later on
#        a plain user can't do this (root owns the directory with installed
#        *.py files)
instfiles2 = env.Install('${libdir}/python/dist-packages/clxx_', instfiles)
env.Ignore("${INSTALL_SANDBOX}${libdir}/python/dist-packages/clxx_", instfiles2)
env.Alias('install-python-clxx',instfiles2)
env.Alias('install', 'install-python-clxx')

# Nest into subdirs
SConscript( 'std/SConscript', exports = ['env'] )
SConscript( 'boost/SConscript', exports = ['env'] )
SConscript( 'cl/SConscript', exports = ['env'] )
SConscript( 'SConscript.clxx_info', exports = ['env'] ) ## FIXME: this doesn't work currently
SConscript( 's11n/SConscript', exports = ['env'] )

# Build in _ldr_
SConscript( '_ldr_/SConscript', exports = ['env'] )

# Local Variables:
# # tab-width:4
# # indent-tabs-mode:nil
# # End:
# vim: set syntax=scons expandtab tabstop=4 shiftwidth=4:
