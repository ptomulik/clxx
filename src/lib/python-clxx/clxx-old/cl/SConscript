
# @COPYRIGHT@
# Licensed under MIT license (LICENSE.txt).

Import(['env'])

from SCons.Util import CLVar

# "Generate" __init__.py
init_py = env.Substfile(['__init__.py.in'], SUBST_DICT = {})
# Generate python modules with swig
libs = env.get('SWIGPY_LIBS', CLVar()) + [ 'OpenCL' ]
modules = [
    'opencl1_0',
    'opencl1_1',
    'opencl1_2',
    'opencl2_0',
    'opencl_mod',
]
# Build modules
modfiles = env.SwigPyModule(modules, SWIGPY_LIBS = libs)
instfiles =  [m for m in modfiles if not str(m).endswith('.cc')] + init_py


# FIXME: this is only for now, elaborate what should be the destination path,
#        also the python packages should be compiled when installed, later on
#        a plain user can't do this (root owns the directory with installed
#        *.py files)
instfiles2 = env.Install('${libdir}/python/dist-packages/clxx_/cl', instfiles)
env.Ignore("${INSTALL_SANDBOX}${libdir}/python/dist-packages/clxx_/cl", instfiles2)
env.Alias('install-python-clxx',instfiles2)
env.Alias('install', 'install-python-clxx')

SConscript('_ldr_/SConscript', exports = ['env'])

# Local Variables:
# # tab-width:4
# # indent-tabs-mode:nil
# # End:
# vim: set syntax=scons expandtab tabstop=4 shiftwidth=4:
