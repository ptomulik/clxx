
# @COPYRIGHT@
# Licensed under MIT license (LICENSE.txt).

Import('env')

from SCons.Util import CLVar

sources =  [
    'context.cpp',
    'context_properties.cpp',
    'device.cpp',
    'device_partition.cpp',
    'device_partition_properties.cpp',
    'devices.cpp',
    'functions.cpp',
    'platform.cpp',
    'platform_layer.cpp',
    'platforms.cpp',
    'program.cpp',
    'program_observer.cpp',
]

headers = [
    'context.hpp',
    'context_properties.hpp',
    'context_property.hpp',
    'device.hpp',
    'device_partition.hpp',
    'device_partition_properties.hpp',
    'device_partition_property.hpp',
    'devices.hpp',
    'functions.hpp',
    'platform.hpp',
    'platform_layer.hpp',
    'platforms.hpp',
    'program.hpp',
    'program_source.hpp',
    'program_sources.hpp',
    'program_binary.hpp',
    'program_binaries.hpp',
    'program_observer.hpp',
]

objects = env.SharedObject(sources)

libs = CLVar('OpenCL clxx_info')

# rlib - "real" libarary
rlib = env.SharedLibrary('../clxx_cl', objects + ['cl/real.cpp'], LIBS = libs)

# tlib - library for testing (OpenCL functions replaced with mocks)
tlib = env.SharedLibrary('../clxx_cl_test', objects + ['cl/mock.cpp'], LIBS = libs)
env.Ignore('../..', tlib)

ilib = env.Install('${libdir}', rlib)
env.Ignore("${INSTALL_SANDBOX}${libdir}", ilib)

iheaders = env.Install("${includedir}/clxx", headers)
env.Ignore("${INSTALL_SANDBOX}${includedir}/clxx", iheaders)

env.Alias('install-libclxx-headers', iheaders)
env.Alias('install-libclxx-lib', ilib)
env.Alias('install-libclxx-dev', ['install-libclxx-headers', 'install-libclxx-lib' ])
env.Alias('install','install-libclxx-dev')

# Local Variables:
# # tab-width:4
# # indent-tabs-mode:nil
# # End:
# vim: set syntax=scons expandtab tabstop=4 shiftwidth=4:
