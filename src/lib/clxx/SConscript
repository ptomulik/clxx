# @COPYRIGHT@
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE

Import('env')

from SCons.Script import GetOption
from SCons.Util import CLVar

objects = env.SharedObject([
    'functions.cpp',
    'platform.cpp',
    'platforms.cpp',
    'device.cpp',
    'devices.cpp',
    'platform_layer.cpp',
    'context_properties.cpp',
    'context.cpp'
])

libs = CLVar('OpenCL clxx_info')
# rlib - "real" libarary
rlib = env.SharedLibrary('../clxx', objects + ['cl/real.cpp'], LIBS = libs)

# tlib - library for testing (OpenCL functions replaced with mocks)
tlib = env.SharedLibrary('../clxx_test', objects + ['cl/mock.cpp'], LIBS = libs)
env.Ignore('../..', tlib)

ilib = env.Install('${libdir}', rlib)
env.Ignore("${INSTALL_SANDBOX}${libdir}", ilib)

ihdr = env.Install("${includedir}/clxx/cl", env.Glob("*.hpp"))
env.Ignore("${INSTALL_SANDBOX}${includedir}/clxx/cl", ihdr)

env.Alias('install-libclxx-headers', ihdr)
env.Alias('install-libclxx-lib', ilib)
env.Alias('install-libclxx-dev', ['install-libclxx-headers', 'install-libclxx-lib' ])
env.Alias('install','install-libclxx-dev')

ihdr = env.Install("${includedir}/clxx", env.Glob("*.hpp"))
env.Ignore("${INSTALL_SANDBOX}${includedir}/clxx", ihdr)

util_ihdr = env.Install("${includedir}/clxx/util", env.Glob("util/*.hpp"))
env.Ignore("${INSTALL_SANDBOX}${includedir}/clxx/util", util_ihdr)

env.Alias('install-libclxx_common-headers', ihdr + util_ihdr)
env.Alias('install-libclxx_common-dev','install-libclxx_common-headers')
env.Alias('install','install-libclxx_common-dev')

SConscript('app/SConscript', exports = ['env'])
#SConscript('cl/SConscript', exports = ['env'])
SConscript('info/SConscript', exports = ['env'])
SConscript('io/SConscript', exports = ['env'])
SConscript('s11n/SConscript', exports = ['env'])

# Local Variables:
# # tab-width:4
# # indent-tabs-mode:nil
# # End:
# vim: set syntax=scons expandtab tabstop=4 shiftwidth=4:
