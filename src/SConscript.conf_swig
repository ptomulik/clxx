
# @COPYRIGHT@
# Licensed under MIT license (LICENSE.txt).

#############################################################################
# Configure 'swig' tool
#############################################################################

Import('conf')

import SConsGnu.AcProgChecks

env = conf.env

# Prefer locally installed swig over the system-wide one
if env.Dir('#ext/swig/bin').exists():
    env.PrependENVPath('PATH', env.Dir('#ext/swig/bin'))

try:
    env['SWIG'] = env['GVARS']['SWIG']
except KeyError:
    try:
        import SCons.Tool.swig
        swigs = SCons.Tool.swig.swigs
    except (ImportError, AttributeError):
        swigs = ['swig', 'swig3.0', 'swig2.0']
    swig = conf.AcCheckProgs(swigs)
    if not swig:
        Return()
    env['SWIG'] = swig

env.Tool('swig')

#############################################################################
env.Append(SWIGFLAGS = [ '-c++' ])
env.Append(SWIGPATH  = [ '#%s' % env.Dir('lib').path ])
env.Append(SWIGPATH  = [ '#%s' % env.Dir('lib/swig-clxx').path ])
env.Append(SWIGPATH  = [ '/usr/include' ])
#############################################################################



# Local Variables:
# # tab-width:4
# # indent-tabs-mode:nil
# # End:
# vim: set syntax=scons expandtab tabstop=4 shiftwidth=4:
