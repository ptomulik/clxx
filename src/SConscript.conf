
# @COPYRIGHT@
# Licensed under MIT license (LICENSE.txt).

Import('conf')

import sys
import SCons.Util
from SConsGnu import CcChecks
from SConsGnu.Cc import CanonCC, CanonCXX

_ccWDeprecatedDeclarations = None;
_ccWNoDeprecatedDeclarations = None;
_cxxWDeprecatedDeclarations = None;
_cxxWNoDeprecatedDeclarations = None;
_ccWerror = None;
_cxxWerror = None;

def HasCcWflag(conf, flag):
    d = conf.env['CC_HAS_FLAG']
    d[flag] = conf.CheckCCFlag(flag, CCFLAGS=['-Werror'])
def HasCxxWflag(conf, flag):
    d = conf.env['CXX_HAS_FLAG']
    d[flag] = conf.CheckCXXFlag(flag, CXXFLAGS=['-Werror'])

def CheckClHVersion(ctx, ver):
    ctx.Message('Checking for OpenCL %s support in CL/cl.h... ' % ver)
    prog = """
#include <CL/cl.h>
int main(int argc, char* argv[]) {
    (void)argc; /* prevent "unused argument argc" warning */
    (void)argv; /* prevent "unused argument argv" warning */
#if CL_VERSION_%s_%s
    return 0;
#else
    return 1;
#endif
}

""" % tuple(ver.split('.'))
    success, outstr = ctx.TryRun(prog,'.c')
    ctx.Result(success)
    return success

def TryLinkClFunction(ctx, fun, code):
    ctx.Message('Checking for OpenCL function %s()... ' % fun)
    prog = """
#include <CL/cl.h>
int main(int argc, char* argv[]) {
    (void)argc; /* prevent "unused argument argc" warning */
    (void)argv; /* prevent "unused argument argv" warning */
    %s
    return 0;
}

""" % code
    flags = { '-CCFLAGS'    : _ccWDeprecatedDeclarations,
              '-CFLAGS'     : _ccWDeprecatedDeclarations,
              'CCFLAGS'     : _ccWNoDeprecatedDeclarations }
    success = CcChecks.TryLinkWithFlags(ctx, flags, prog, '.c')
    ctx.Result(success)
    return success;

def TryCompileClFunctionWithFlags(ctx, flags, fun, code, **overrides):
    caller_did_show_result = ctx.did_show_result
    if not caller_did_show_result:
        ctx.Message('Checking whether %s compiles with %s' % (fun, flag))
        ctx.did_show_result = 1
    prog = """
#include <CL/cl.h>
int main(int argc, char* argv[]) {
    (void)argc; /* prevent "unused argument argc" warning */
    (void)argv; /* prevent "unused argument argv" warning */
    %s
    return 0;
}

""" % code
    success = CcChecks.TryCompileWithFlags(ctx, flags, prog, **overrides)
    if not caller_did_show_result:
        ctx.did_show_result = 0
        ctx.Result(success)
    return success

def CheckClFunctionDeclared(ctx, fun, code, **overrides):
    caller_did_show_result = ctx.did_show_result
    if not caller_did_show_result:
        ctx.Message('Checking whether %s() is declared... ' % fun)
        ctx.did_show_result = 1
    flags = { 'CCFLAGS'     : _ccWNoDeprecatedDeclarations,
              '-CCFLAGS'    : _ccWDeprecatedDeclarations,
              '-CFLAGS'     : _ccWDeprecatedDeclarations }
    success = TryCompileClFunctionWithFlags(ctx, flags, fun, code, **overrides)
    if not caller_did_show_result:
        ctx.did_show_result = 0
        ctx.Result(success)
    return success

def CheckClFunctionNotDeprecated(ctx, fun, code, **overrides):
    caller_did_show_result = ctx.did_show_result
    if not caller_did_show_result:
        ctx.Message('Checking whether %s() is not deprecated... ' % fun)
        ctx.did_show_result = 1
    flags = { 'CCFLAGS'     : _ccWDeprecatedDeclarations + ' ' + _ccWerror,
              '-CCFLAGS'    : _ccWNoDeprecatedDeclarations,
              '-CFLAGS'     : _ccWNoDeprecatedDeclarations }
    success = TryCompileClFunctionWithFlags(ctx, flags, fun, code, **overrides)
    if not caller_did_show_result:
        ctx.did_show_result = 0
        ctx.Result(success)
    return success

def CharacterizeClFunction(conf, fun, code):
    # Check if a function is declared in CL/cl.h
    if conf.CheckClFunctionDeclared(fun, code):
        conf.Define('CLXX_IS_DECLARED_%s' % fun, 1, 'OpenCL header declares %s()' % fun)
        # If declared, check whether it's not deprecated
        if not conf.CheckClFunctionNotDeprecated(fun, code):
            conf.Define('CLXX_IS_DEPRECATED_%s' % fun, 1, '%s is declared as deprecated' % fun)
        # ... and whether libOpenCL.so has it as well
        if conf.TryLinkClFunction(fun, code):
            conf.Define('CLXX_IS_LINKABLE_%s' % fun, 1, 'libOpenCL had the %s when libclxx was build' % fun)

conf.AddTests(CcChecks.Tests())
conf.AddTests({ 'CheckClHVersion' : CheckClHVersion })
conf.AddTests({ 'TryLinkClFunction' : TryLinkClFunction })
conf.AddTests({ 'TryCompileClFunctionWithFlags' : TryCompileClFunctionWithFlags })
conf.AddTests({ 'CheckClFunctionDeclared' : CheckClFunctionDeclared })
conf.AddTests({ 'CheckClFunctionNotDeprecated' : CheckClFunctionNotDeprecated })

conf.env['CC_HAS_FLAG'] = {}
conf.env['CXX_HAS_FLAG'] = {}

if sys.maxsize > 2**32:
    conf.Define( 'CLXX_ARCH_64BIT' )

cc = CanonCC(conf.env)
if cc == 'clang':
    HasCcWflag(conf, '-Wdeprecated-register')
    HasCcWflag(conf, '-Wdeprecated-declarations')
    HasCcWflag(conf, '-Wunused-parameter')
    _ccWDeprecatedDeclarations = '-Wdeprecated-declarations'
    _ccWNoDeprecatedDeclarations = '-Wno-deprecated-declarations'
    _ccWerror = '-Werror'
elif cc == 'gcc':
    HasCcWflag(conf, '-Wmaybe-uninitialized')
    HasCcWflag(conf, '-Wdeprecated-declarations')
    _ccWDeprecatedDeclarations = '-Wdeprecated-declarations'
    _ccWNoDeprecatedDeclarations = '-Wno-deprecated-declarations'
    _ccWerror = '-Werror'

cxx = CanonCXX(conf.env)
if cxx == 'clang++':
    HasCxxWflag(conf, '-Wdeprecated-register')
    HasCxxWflag(conf, '-Wdeprecated-declarations')
    HasCxxWflag(conf, '-Wunused-parameter')
    _cxxWDeprecatedDeclarations = '-Wdeprecated-declarations'
    _cxxWNoDeprecatedDeclarations = '-Wno-deprecated-declarations'
    _cxxWerror = '-Werror'
elif cxx == 'g++':
    HasCxxWflag(conf, '-Wmaybe-uninitialized')
    HasCxxWflag(conf, '-Wdeprecated-declarations')
    _cxxWDeprecatedDeclarations = '-Wdeprecated-declarations'
    _cxxWNoDeprecatedDeclarations = '-Wno-deprecated-declarations'
    _cxxWerror = '-Werror'

# Prepare modified environment for the OpenCL checks
orgEnv = conf.env
conf.env = orgEnv.Clone()
conf.env.AppendUnique(LIBS = ['OpenCL'])

if conf.CheckClHVersion('1.0'):
    conf.Define( 'HAVE_OPENCL_CL_H_VERSION_1_0', 1, 'CL/cl.h supports OpenCL 1.0')
if conf.CheckClHVersion('1.1'):
    conf.Define( 'HAVE_OPENCL_CL_H_VERSION_1_1', 1, 'CL/cl.h supports OpenCL 1.1')
if conf.CheckClHVersion('1.2'):
    conf.Define( 'HAVE_OPENCL_CL_H_VERSION_1_2', 1, 'CL/cl.h supports OpenCL 1.2')
if conf.CheckClHVersion('2.0'):
    conf.Define( 'HAVE_OPENCL_CL_H_VERSION_2_0', 1, 'CL/cl.h supports OpenCL 2.0')

#
# Checking for the existence of certain OpenCL functions.
#
_clCreateSubDevices = \
"""clCreateSubDevices(
        (cl_device_id)NULL,
        (const cl_device_partition_property*)NULL,
        0u,
        (cl_device_id*)NULL,
        (cl_uint*)NULL);
"""
CharacterizeClFunction(conf, 'clCreateSubDevices', _clCreateSubDevices)
##if conf.TryLinkClFunction('clCreateSubDevices', _clCreateSubDevices):
##    conf.Define('HAVE_OPENCL_clCreateSubDevices', 1, 'OpenCL has the clCreateSubDevices()')

_clRetainDevice = \
"""clRetainDevice((cl_device_id)NULL);
"""
CharacterizeClFunction(conf, 'clRetainDevice', _clRetainDevice)
##if conf.TryLinkClFunction('clRetainDevice', _clRetainDevice):
##    conf.Define('HAVE_OPENCL_clRetainDevice', 1, 'OpenCL has the clRetainDevice()')

_clReleaseDevice = \
"""clReleaseDevice((cl_device_id)NULL);
"""
CharacterizeClFunction(conf, 'clReleaseDevice', _clReleaseDevice)
##if conf.TryLinkClFunction('clReleaseDevice', _clReleaseDevice):
##    conf.Define('HAVE_OPENCL_clReleaseDevice', 1, 'OpenCL has the clReleaseDevice()')

_clCreateProgramWithBuiltInKernels = \
"""clCreateProgramWithBuiltInKernels(
        (cl_context)NULL,
        0,
        (const cl_device_id*)NULL,
        (const char*)NULL,
        (cl_int*)NULL);
"""
CharacterizeClFunction(conf, 'clCreateProgramWithBuiltInKernels', _clCreateProgramWithBuiltInKernels)
##if conf.TryLinkClFunction('clCreateProgramWithBuiltInKernels', _clCreateProgramWithBuiltInKernels):
##    conf.Define('HAVE_OPENCL_clCreateProgramWithBuiltInKernels', 1,
##                'OpenCL has the clCreateProgramWithBuiltInKernels()')

_clCompileProgram = \
"""clCompileProgram(
        (cl_program)NULL,
        0,
        (const cl_device_id*)NULL,
        (const char*)NULL,
        0,
        (const cl_program*)NULL,
        (const char**)NULL,
        (void(CL_CALLBACK*)(cl_program,void*))NULL,
        NULL);
"""
CharacterizeClFunction(conf, 'clCompileProgram', _clCompileProgram)
##if conf.TryLinkClFunction('clCompileProgram', _clCompileProgram):
##    conf.Define('HAVE_OPENCL_clCompileProgram', 1,
##                'OpenCL has the clCompileProgram()')

_clLinkProgram = \
"""clLinkProgram(
        (cl_context)NULL,
        0,
        (const cl_device_id*)NULL,
        (const char*)NULL,
        0,
        (const cl_program*)NULL,
        (void(CL_CALLBACK*)(cl_program,void*))NULL,
        NULL,
        NULL);
"""
CharacterizeClFunction(conf, 'clLinkProgram', _clLinkProgram)
##if conf.TryLinkClFunction('clLinkProgram', _clLinkProgram):
##    conf.Define('HAVE_OPENCL_clLinkProgram', 1,
##                'OpenCL has the clLinkProgram()')


_clUnloadPlatformCompiler = \
"""clUnloadPlatformCompiler((cl_platform_id)NULL);
"""
CharacterizeClFunction(conf, 'clUnloadPlatformCompiler', _clUnloadPlatformCompiler)
##if conf.TryLinkClFunction('clUnloadPlatformCompiler', _clUnloadPlatformCompiler):
##    conf.Define('HAVE_OPENCL_clUnloadPlatformCompiler', 1,
##                'OpenCL has the clUnloadPlatformCompiler()')

_clCreateCommandQueue = """
clCreateCommandQueue( (cl_context)0,
                      (cl_device_id)0,
                      (cl_command_queue_properties)0,
                      (cl_int*)NULL );
"""
CharacterizeClFunction(conf, 'clCreateCommandQueue', _clCreateCommandQueue)
##if conf.TryLinkClFunction('clCreateCommandQueue', _clCreateCommandQueue):
##    conf.Define('HAVE_OPENCL_clCreateCommandQueue', 1, 'OpenCL has the clCreateCommandQueue()')

_clCreateCommandQueueWithProperties = \
"""clCreateCommandQueueWithProperties(  (cl_context)0,
                                        (cl_device_id)0,
                                        (cl_queue_properties const*)NULL,
                                        (cl_int*)NULL );
"""
CharacterizeClFunction(conf, 'clCreateCommandQueueWithProperties', _clCreateCommandQueueWithProperties)
##if conf.TryLinkClFunction('clCreateCommandQueueWithProperties',
##                          _clCreateCommandQueueWithProperties):
##    conf.Define('HAVE_OPENCL_clCreateCommandQueueWithProperties', 1,
##                'OpenCL has the clCreateCommandQueueWithProperties()')


_clCreateCommandQueue = \
"""clCreateCommandQueue( (cl_context)0,
                         (cl_device_id)0,
                         (cl_command_queue_properties)0,
                         (cl_int*)NULL );
"""
CharacterizeClFunction(conf, "clCreateCommandQueue", _clCreateCommandQueue)

# Restore the original environment
conf.env = orgEnv

# --without-cl-egl
try:
    if conf.env['GVARS']['WITHOUT_CL_EGL']:
        conf.Define('CLXX_WITHOUT_CL_EGL', 1, 'Do not inclue CL/cl_egl.h')
except KeyError:
    pass

## # FIXME: for MAC-OS the header is OpenCL/cl.h
## conf.req_headers.append(('CL/cl.h','<>','C'))
## conf.req_libs.append({ 'library' : ['OpenCL'],
##                        'autoadd' : 0,
##                        'header'  : '#include <CL/cl.h>' })

# Local Variables:
# # tab-width:4
# # indent-tabs-mode:nil
# # End:
# vim: set syntax=scons expandtab tabstop=4 shiftwidth=4:
